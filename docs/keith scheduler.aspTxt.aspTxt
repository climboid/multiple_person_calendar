<%@ Language=VBScript %>
<!--#include file="../include/init_connection.asp"-->
<!--#include file="../common/checkpersoncookie.asp"-->
<!--#include file="../SCConfig/SCConfig_Calendar.asp"-->
<%
'********************************************************************************************************************************
'- This page is used to fetch and display event participants' freebusy status as per their Outlook calendar
'- Data is fetched by a function call to PersonAvailability.dll
'  *The DLL should be registered on the server on which this page is present.
'
'- Constants used from SCConfig_Calendar.asp:
'   1. SC_CALENDAR_FB_REQUEST_URL - url to which the PersonAvailability.dll will make a GET request.
'                                   It is generally the url of the server which provides PUBLIC folder access in
'                                   the organisation's MS Exchange setup.
'
'   *The freebusy data can be accessed only by logging into a mailbox. 
'   2. SC_CALENDAR_MAILBOX_ID - The user id of the mailbox being used.
'   3. SC_CALENDAR_MAILBOX_PASSWORD - password of the mailbox being used.
'
'- Parameters needed to get the freebusy data:
'   1.start_date - data from this date will be fetched 
'   2.end_date - data till this date will be fetched 
'   3.start_time - data from this time on the start date will be fetched
'   4.end_time - data till this time on the start date will be fetched
'   5.time_zone - start and end date & time provided are as per this time zone. Data fetched will also be as per this time zone
'   6.interval - in minutes. Freebusy status for every "interval" no. of minutes will be fetched.
'   7.email_id - string of email IDs of event participants delimited by a colon
'
'- Data fetched will from the start of the start_date's previous day to the end of the end_date's next day. 
'*******************************************************************************************************************************
Dim oXmlDom
Dim objNodeList
dim objItemNode
dim objNodeName
dim objNodeNameList
dim objNodeDays
dim objNodeDay
dim objNodeTimes
dim objNodeTime
dim oPersonStatus
dim start_date
dim end_date
dim start_time
dim end_time
dim email_id
dim arrEmailId
dim time_zone
dim interval
dim nPersons
dim nDays
dim nName
dim strXML
dim status_color
dim status_time
dim status_day
dim cmdPersonEmail
dim cmdTimeZone
dim rdset

on error resume next
email_id = Request.QueryString("person_id")
start_date = Request.QueryString("start")
end_date = Request.QueryString("end")
start_time = "00:00:00" 'default start time of a day
end_time = "23:59:59" 'default end time of a day
time_zone = Request.QueryString("time_zone")
interval = "30" ' in minutes-minimum period for an event in SupportCentral caledar.

if not IsValidInputString(start_date) or not IsValidInputString(end_date) or not isValidNumeric(time_zone) then
  %><SCRIPT LANGUAGE=javascript>
		    <!--
			    window.open("/sup_msg_page.asp?msgid=0","_top");
		    //-->
		  </SCRIPT>
		  <!--#include file="../include/connection_close.asp"-->	
  <%response.end
end if
start_date = dateadd("d",-1,cdate(start_date))
end_date = dateadd("d",1,cdate(end_date))

arrEmailId = split(email_id,";")
email_id=""
if err.number<>0 then
  %><SCRIPT LANGUAGE=javascript>
		    <!--
			    window.open("/sup_msg_page.asp?msgid=0","_top");
		    //-->
		  </SCRIPT>
		  <!--#include file="../include/connection_close.asp"-->	
  <%response.end
end if

for i=0 to ubound(arrEmailId)-1
	if not isnull(arrEmailId(i)) and arrEmailId(i)<>"" then
	 if not isValidNumeric(arrEmailId(i)) then
       %><SCRIPT LANGUAGE=javascript>
		    <!--
			    window.open("/sup_msg_page.asp?msgid=0","_top");
		    //-->
		  </SCRIPT>
		  <!--#include file="../include/connection_close.asp"-->	
		<%response.end
	  end if	
	end if
next

set cmdPersonEmail = server.CreateObject("ADODB.command")
set cmdPersonEmail.ActiveConnection = connection

cmdPersonEmail.CommandText = "SELECT email FROM t_person WHERE person_id = ?"
cmdPersonEmail.Parameters.append cmdPersonEmail.CreateParameter("person_id",131, &H0001,14)
cmdPersonEmail.Prepared = true
for i=0 to ubound(arrEmailId)
 if arrEmailId(i) <> "" and not isempty(arrEmailId(i)) then
   cmdPersonEmail.Parameters.Item(0).Value = arrEmailId(i)
   set rdset = cmdPersonEmail.Execute
   if not rdset.eof then
    email_id = rdset(0) & ";" & email_id
   end if
   set rdset = nothing
 end if
next
set cmdPersonEmail = nothing
email_id = mid(email_id,1,len(email_id)-1)

set cmdTimeZone = server.CreateObject("ADODB.command")
set cmdTimeZone.ActiveConnection = connection

cmdTimeZone.CommandText = "SELECT time_gmt_diff FROM t_time_zones WHERE	time_zone_id = ?"
cmdTimeZone.Parameters.append cmdTimeZone.CreateParameter("time_zone_id",131, &H0001,4,cint(time_zone))
cmdTimeZone.Prepared = true
set rdset = cmdTimeZone.Execute
if not rdset.eof then
 time_zone = rdset(0) 
else
 time_zone = "-05:00"
end if
set rdset = nothing
set cmdTimeZone = nothing


nDays = datediff("d",cdate(end_date),cdate(start_date)) + 1

set oXmlDom = server.CreateObject("MSXML2.DOMdocument")
set oPersonStatus = CreateObject("PersonAvailability.clsGetStatus")
strXML = oPersonStatus.getAvailability(SC_CALENDAR_FB_REQUEST_URL,cstr(email_id),cstr(start_date),cstr(end_date),cstr(start_time),cstr(end_time),cstr(time_zone),cstr(interval),SC_CALENDAR_MAILBOX_ID,SC_CALENDAR_MAILBOX_PASSWORD)
oXmlDom.loadXML(strXML)
set oPersonStatus = nothing
set objNodeList = oXmlDom.selectnodes("//participant")
set objNodeNameList = oXmlDom.getElementsByTagName("name")

%>
<HTML>
<HEAD>
<title>Participant Availability</title>
<link href="../scui/0.4/css/allStyles.css" rel="stylesheet" type="text/css"  />
<style>
.height_font{height:20px;font-family:Arial, Helvetica, sans-serif; font-size:12px;}
.purple-background{background-color:#993366;}
.light-blue-background{background-color:#ECEDFC;}
.medium-blue-background{background-color:#ABC0DB;}
</style>
<script>
 function fnScroll(){
  fb_data_view.scrollLeft = 2250;//to scroll to the section where data for the selected start date is present.
 } 
</script>
</HEAD>
<BODY onload="javascript:fnScroll();">
<div width=100%>
<table border="0" cellspacing="1" width="100%" class="fullBorder" style="table-layout:fixed;">
<tr>
 <td width=100px height=48px class="light-gray-background">&nbsp;</td>
 <!-- objNodeNameList must be each person fetches -->
 <td rowspan="<%=objNodeNameList.length+2%>" class="light-gray-background">
 <div style="width:100%;overflow-x:scroll;" id="fb_data_view">
  <table width=100% border="0" cellspacing="1" style="background-color:white;">
        
      <!-- finds how many days are in the data set -->
	  <%set objNodeDays = objNodeList.item(0).selectnodes("day")%>
        <tr>
          <!-- creates a column header for each day -->
		  <%for each objNodeDay in objNodeDays
            status_day = objNodeDay.Attributes(0).Text%> 
           <td colspan="48" class="light-gray-background height_font">&nbsp;&nbsp;<%=Formatdatetime(status_day,1)%></td>  
          <%next%> 
        </tr>
        <tr>
        <!-- time headers, not quite sure how these are built. -->
		<% for each objNodeDay in objNodeDays
            set objNodeTimes = objNodeDay.selectnodes("time")
			 for each objNodeTime in objNodeTimes
			  status_time = objNodeTime.Attributes(0).Text
			  if instr(1,status_time,":00")>0 then%>
		       <td class="light-gray-background height_font" style="font-size:11px;" colspan="2"><%=status_time%></td>
		     <%end if
		     next
		   next%> 
		</tr>
   		<%for each objItemNode in objNodeList
		    set objNodeName = objItemNode.selectsinglenode("name")
		    set objNodeDays = objItemNode.selectnodes("day")
			    
		 ' <tr>
		'	  <td class="dark-gray-background">&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;</td>
		'	</tr>
			%><tr width="600px"><%
			'	<td width=25% class="green-background"><div><%=objNodeName.text</div></td>
			 for each objNodeDay in objNodeDays
			   set objNodeTimes = objNodeDay.selectnodes("time")
			   for each objNodeTime in objNodeTimes
			     if objNodeTime.text = "2" then'busy
			        status_color = "blue-background"
			     elseif objNodeTime.text = "1" then' tentative
			        status_color = "medium-blue-background"
			     elseif objNodeTime.text = "3" then 'out of office
			        status_color = "purple-background"
			     elseif objNodeTime.text = "4" then' data not available
			        status_color = "dark-gray-background"
			     else 'available  
			        status_color = "lighter-gray-background"
			     end if%>
				<td style="height:20px;" class="<%=status_color%>">&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;</td>
			 <%next	
			  next%>
			</tr>
		<%next%>
	</table>
   </div>
  </td>
 </tr> 
 <%for i=1 to objNodeNameList.length%>
 <tr>
  <td style="width:100px;height:22px;font-family:Arial, Helvetica, sans-serif; font-size:11px;overflow-x:hidden;" class="green-background" title="<%=objNodeNameList.item(i-1).text%>">&nbsp;<%=mid(objNodeNameList.item(i-1).text,1,15)%></td>
 </tr>
 <%next%>  		
 <tr id="dummy">
  <td width=100px class="light-gray-background">&nbsp;</td>
 </tr>
</table>
<table border="0">
 <tr>
  <td><div style="width:14px;height:14px;" class="lighter-gray-background">&nbsp;</div></td><td class="height_font">Available&nbsp;&nbsp;</td>
  <td><div style="width:14px;height:14px;" class="blue-background">&nbsp;</div></td><td class="height_font">Busy&nbsp;&nbsp;</td>
  <td><div style="width:14px;height:14px;" class="medium-blue-background">&nbsp;</div></td><td class="height_font">Tentative&nbsp;&nbsp;</td>
  <td><div style="width:14px;height:14px;" class="purple-background">&nbsp;</div></td><td class="height_font">Out of Office&nbsp;&nbsp;</td>
  <td><div style="width:14px;height:14px;" class="dark-gray-background">&nbsp;</div></td><td class="height_font">Information not available</td>
  
 </tr>
</table>
<span class="red_small" style="font-family:Arial, Helvetica, sans-serif; font-size:11px;">Note: Timings are according to <b>GMT <%=time_zone%></b> timezone.</span>
<table style="width:100%;table-layout:fixed;">
 <tr>
  <td width="45%">&nbsp;</td>
  <td width="10%" class="blue_button_small">&nbsp;<a href=# style="font-family:Arial, Helvetica, sans-serif;font-size:12px;font-weight:bold;" id='close_confirm' onClick="javascript:window.close();">Close</a>&nbsp;</td>
  <td width="45%">&nbsp;</td>
 </tr> 
</table>
</div>

</BODY>
</HTML>
